plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.4'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'org.graalvm.buildtools.native' version '0.10.6'
}

group = 'com.github.ansarhun'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(23)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

processResources {
	filesMatching(["application*.yml"]) {
		expand(project.properties)
	}
}

repositories {
	mavenCentral()
}

def fabric8Version = '7.1.0'

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'

	implementation "io.fabric8:kubernetes-client:${fabric8Version}"

	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	annotationProcessor "io.fabric8:crd-generator-apt:${fabric8Version}"

	testAndDevelopmentOnly 'org.springframework.boot:spring-boot-devtools'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'io.projectreactor:reactor-test'
	testImplementation 'org.testcontainers:junit-jupiter'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	testImplementation 'org.testcontainers:k3s'
	testImplementation "org.testcontainers:minio"

	testImplementation "io.fabric8:kubernetes-server-mock:${fabric8Version}"

	// for native hints
	implementation 'org.reflections:reflections:0.10.2'
}

tasks.named('test') {
	useJUnitPlatform()
}

graalvmNative {
	binaries.all {
		// Added for fabric8
		buildArgs.add("-H:+AddAllCharsets")

		// For dev builds
		buildArgs.add("-Ob")
	}
}

tasks.named("bootBuildImage") {
	environment = [
		// Added for fabric8
		"BP_NATIVE_IMAGE_BUILD_ARGUMENTS": "-H:+AddAllCharsets"
	]
}
